#autoload
#
# Helper function for _acdcli.
#
# Completes to node IDs of trashed nodes (top-level only, no recursion).
#
# Authors:
#   Zhiming Wang <zmwangx@gmail.com>
#

local nodes # array with element of the form node_id:node_path suitable for _describe
__acdcli_retrieve_timeout
nodes=()
command $timeout_cmd_args[@] acd_cli --check=none list-trash 2>/dev/null | \
    while IFS= read -r line; do
        # line looks like: [17Be8nxHT9-dT0CxfjnQJQ] [T] Documents/
        node_id=${${line[(ws: :)1]}//[\[\]]/}
        node_path=${line[(ws: :)3,-1]}
        nodes+="$node_id:$node_path"
    done
if __acdcli_zsh_version_no_less_than 5.0.8; then
    # -V to unsort node IDs and present as returned by list-trash
    # Only available in 5.0.8 or later, though
    _describe -V "trashed node" nodes
else
    _describe "trashed node" nodes
fi
