# -*- mode: sh; -*-
#
# A prompt theme optimized for iTerm2 and Solarized dark. Use with Monaco 10pt
# (without anti-aliasing) for best experience.
#
# Authors:
#   Zhiming Wang
#
# Screenshots:
#   https://i.imgur.com/nCBK8ZB.png
#

pmodload 'helper'

function prompt_zmwangx_precmd {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    (( $+functions[git-info] )) && git-info
}

function prompt_zmwangx_setup {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    prompt_opts=(cr percent subst)

    autoload -Uz add-zsh-hook
    add-zsh-hook precmd prompt_zmwangx_precmd

    # git-info parameters
    zstyle ':prezto:module:git:info' verbose 'yes'
    zstyle ':prezto:module:git:info:branch' format '%F{green}[%b]%f'
    zstyle ':prezto:module:git:info:position' format '%F{green}[%p]%f'
    zstyle ':prezto:module:git:info:commit' format '%F{green}[%.7c]%f'
    zstyle ':prezto:module:git:info:dirty' format '%F{red}*%f'
    zstyle ':prezto:module:git:info:action' format ' %B%F{yellow}%s%f%b'
    zstyle ':prezto:module:git:info:keys' format \
           'prompt' '$(coalesce "%b" "%p" "%c")%D%s ' \
           'rprompt' ''

    local rootseg='%(!.%B%F{red}ROOT%f%b .)'
    local hostseg='%B%F{blue}${SSH_CLIENT:+%M }%f%b'
    local timeseg='%F{yellow}%*%f '
    local pathseg='%F{cyan}[%~]%f '
    local gitseg='${git_info:+${(e)git_info[prompt]}}'
    local pythonseg='%F{magenta}${VIRTUAL_ENV:+py${VIRTUAL_ENV_PYTHON_VERSION}:${VIRTUAL_ENV##*/} }%f'
    local jobseg='%(1j.%F{blue}(%j)%f .)'
    local arrowseg='%(?.%F{green}.%F{red})>>>%f'
    PROMPT="$rootseg$hostseg$timeseg$pathseg$gitseg$pythonseg$jobseg$arrowseg
"
    RPROMPT=''
    SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_zmwangx_setup
